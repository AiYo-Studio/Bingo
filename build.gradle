import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

project.ext {
    ver = "1.3.9-SNAPSHOT"
}

dependencies {
    implementation project(":common")
    implementation project(":module:module-pixelmon-legacy")
    implementation project(":module:module-pixelmon-native")
}

allprojects {
    apply plugin: 'java'

    archivesBaseName = "Bingo"
    version = ""

    sourceCompatibility = targetCompatibility = '1.8'
    compileJava {
        sourceCompatibility = targetCompatibility = '1.8'
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name = "AyStudio-Repository"
            url = "https://repo.mc9y.com/snapshots"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.24")
        annotationProcessor("org.projectlombok:lombok:1.18.24")
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.yml'
            filter ReplaceTokens, tokens: ["version": project.property("ver")]
            duplicatesStrategy DuplicatesStrategy.INCLUDE
        }
    }

    processResources {
        setFilteringCharset("UTF-8")
        expand project.properties
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

tasks.register('allJar', Jar) {
    dependsOn subprojects.tasks["build"]
    baseName = 'Bingo-' + project.property("ver")
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

tasks.jar.enabled = false

artifacts {
    archives allJar
}